task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
bintray {
    user = project.hasProperty("bintrayUser")? project.property("bintrayUser") : ""
    key = project.hasProperty("bintrayKey")? project.property("bintrayKey") : ""
    configurations = ['archives']
    pkg {
        repo = 'tools'
        name = 'smoke'
        userOrg = 'ez'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        version {
            name = project.version
            desc = 'Gradle Bintray Plugin 1.0 final'
            vcsTag = '1.3.0'
        }
    }
    publications = ['MyPublication']
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "0"
            name "Elton Zhong"
            email "eltonzhongzxn@gmail.com"
        }
    }

    scm {
        url "https://github.com/eltonzhong/smoke"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Validation framework')
                root.appendNode('name', 'smoke')
                root.appendNode('url', 'https://github.com/eltonzhong/smoke')
                root.children().last() + pomConfig
            }
        }
    }
}